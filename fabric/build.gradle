plugins {
    id 'fabric-loom'
    id 'com.github.johnrengelman.shadow'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = maven_group
archivesBaseName = archives_base_name

repositories {
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
}

runClient.doFirst {
    args = ['--username', "henkelmax${new Random().nextInt(1000)}"]
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    implementation project(':core')
    shadow project(':core')

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 8
}

java {
    withSourcesJar()
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = 'shadow-dev'
    dependencies {
        exclude(dependency('com.google.code.findbugs:jsr305'))
        exclude(dependency('com.mojang:brigadier'))
    }
}

remapJar {
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile.get()
}
