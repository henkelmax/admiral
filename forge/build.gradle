plugins {
    id 'java'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
    id 'com.github.johnrengelman.shadow'
    id 'com.modrinth.minotaur'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

group = maven_group
archivesBaseName = archives_base_name
version = "${library_version}+${minecraft_version}"

minecraft {
    mappings channel: 'official', version: minecraft_version
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net' }
}

configurations {
    shadowMc.extendsFrom minecraftLibrary
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    shadowMc project(':core')
    shadowMc project([path: ':minecraft', configuration: 'namedElements'])

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

processResources {
    filesMatching('**/*.toml') {
        expand 'version': version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}

shadowJar {
    configurations = [project.configurations.shadowMc]
    archiveClassifier = ''
    dependencies {
        exclude(dependency('com.google.code.findbugs:jsr305'))
        exclude(dependency('com.mojang:brigadier'))
    }
}
shadowJar.dependsOn('reobfJar')

reobf {
    shadowJar {}
}

publishing {
    publications {
        admiralFabric(MavenPublication) {
            artifactId archives_base_name
            artifact(jar) {
                builtBy build
                classifier null
            }
        }
    }
    repositories {
        maven {
            name = 'henkelmax.public'
            url = 'https://maven.maxhenkel.de/repository/public'
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

modrinth {
    token = file("${rootDir}/modrinth_token.txt").exists() ? file("${rootDir}/modrinth_token.txt").text : ''
    projectId = modrinth_id
    versionNumber = "${version}+forge"
    versionName = "Admiral Forge ${version}"
    uploadFile = jar
    versionType = release_type.toUpperCase()
    changelog = rootProject.file('changelog.md').text
    gameVersions = supported_minecraft_versions.split(',').collect { it.trim() }
    loaders = ['forge']
}
tasks.modrinth.dependsOn(build)
