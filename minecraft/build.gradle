plugins {
    id 'java'
    id 'maven-publish'
    id 'fabric-loom'
    id 'com.gradleup.shadow'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

base {
    archivesName = archives_base_name
}
group = maven_group
version = "${library_version}+${minecraft_version}"

repositories {
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modCompileOnly "net.fabricmc:fabric-loader:${loader_version}"

    shadow project(':core')

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 21
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = 'shadow-dev'
    dependencies {
        exclude(dependency('com.google.code.findbugs:jsr305'))
        exclude(dependency('com.mojang:brigadier'))
    }
}

remapJar {
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile.get()
}

tasks.register('shadedSourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from project(':core').sourceSets.main.allSource
}
tasks.shadedSourcesJar.dependsOn compileJava
tasks.shadedSourcesJar.dependsOn shadowJar

publishing {
    publications {
        admiralFabric(MavenPublication) {
            artifactId archives_base_name
            artifact(shadowJar) {
                builtBy build
                classifier null
            }
            artifact shadedSourcesJar
        }
    }
    repositories {
        maven {
            name = 'henkelmax.public'
            url = 'https://maven.maxhenkel.de/repository/public'
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}
